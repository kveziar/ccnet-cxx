cmake_minimum_required(VERSION 3.0)

cmake_policy(VERSION 3.0)

project(ccnet-cxx VERSION 0.1.0 LANGUAGES CXX)

# project variables
set(CCNET_PACKAGE_NAME ${PROJECT_NAME})
set(CCNET_EXPORT_NAME ${CCNET_PACKAGE_NAME}-targets)
set(CCNET_CONFIG_FILENAME ${CCNET_PACKAGE_NAME}-config.cmake)
set(CCNET_CONFIGVERSION_FILENAME ${CCNET_PACKAGE_NAME}-config-version.cmake)
set(CCNET_CMAKE_DIR cmake)
set(CCNET_TARGET_NAME ${PROJECT_NAME})

set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/lib)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/lib)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/bin)

# set output directories for each build configuration
foreach(CONFIG ${CMAKE_CONFIGURATION_TYPES})
	string(TOUPPER ${CONFIG} UPPER_CONFIG)
	set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_${UPPER_CONFIG} ${CMAKE_ARCHIVE_OUTPUT_DIRECTORY}/${CONFIG})
	set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_${UPPER_CONFIG} ${CMAKE_LIBRARY_OUTPUT_DIRECTORY}/${CONFIG})
	set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_${UPPER_CONFIG} ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${CONFIG})
endforeach(CONFIG CMAKE_CONFIGURATION_TYPES)

add_subdirectory(src)

configure_file(
	${CCNET_CMAKE_DIR}/${CCNET_CONFIG_FILENAME}.in
	${CCNET_CMAKE_DIR}/${CCNET_CONFIG_FILENAME}
	@ONLY
)

include(CMakePackageConfigHelpers)
write_basic_package_version_file(${CCNET_CMAKE_DIR}/${CCNET_CONFIGVERSION_FILENAME}
	COMPATIBILITY SameMajorVersion
)

export(EXPORT ${CCNET_EXPORT_NAME} FILE ${CCNET_CMAKE_DIR}/${CCNET_EXPORT_NAME}.cmake)
export(PACKAGE ${CCNET_PACKAGE_NAME})

if(WIN32)
	install(FILES
		${PROJECT_BINARY_DIR}/${CCNET_CMAKE_DIR}/${CCNET_CONFIG_FILENAME}
		${PROJECT_BINARY_DIR}/${CCNET_CMAKE_DIR}/${CCNET_CONFIGVERSION_FILENAME}
		DESTINATION ${CCNET_CMAKE_DIR}
	)
	install(EXPORT ${CCNET_EXPORT_NAME}
		DESTINATION ${CCNET_CMAKE_DIR}
	)
else(WIN32)
	install(FILES
		${PROJECT_BINARY_DIR}/${CCNET_CMAKE_DIR}/${CCNET_CONFIG_FILENAME}
		${PROJECT_BINARY_DIR}/${CCNET_CMAKE_DIR}/${CCNET_CONFIGVERSION_FILENAME}
		DESTINATION lib/${CCNET_CMAKE_DIR}/${CCNET_PACKAGE_NAME}
	)
	install(EXPORT ${CCNET_EXPORT_NAME}
		DESTINATION lib/${CCNET_CMAKE_DIR}/${CCNET_PACKAGE_NAME}
	)
endif(WIN32)

install(DIRECTORY
	include
	DESTINATION .
)
